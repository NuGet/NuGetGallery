PREFIX nuget: <http://schema.nuget.org/schema#>
PREFIX catalog: <http://schema.nuget.org/catalog#>

CONSTRUCT
{
    ?resource a nuget:Resolver ;
	          nuget:id @id ;
			  nuget:package ?resource_package .

	?resource_package nuget:version ?version ;
	                  nuget:dependencies ?resource_dependencies ;
					  catalog:commitId ?commitId ;
					  catalog:timeStamp ?timeStamp .

    ?resource_dependencies nuget:group ?resource_dependency_group .

    ?resource_dependency_group nuget:dependency ?resource_dependency ;
                               nuget:targetFramework ?dependency_group_targetFramework ;
					           nuget:name ?dependency_group_name ;
					           nuget:property ?resource_dependency_group_property .

    ?resource_dependency_group_property nuget:name ?dependency_group_property_name ;
                                        nuget:value ?dependency_group_property_value .

	?resource_dependency nuget:id ?dependency_id ;
				         nuget:range ?dependency_version ;
				         nuget:property ?resource_dependency_property .

	?resource_dependency_property nuget:name ?dependency_property_name ;
					              nuget:value ?dependency_property_value .
}
WHERE
{
	BIND (IRI(CONCAT(@base, @id, @extension)) AS ?resource)

    ?package a nuget:Package ;
	         nuget:version ?version ;
             nuget:id ?id ;
			 catalog:commitId ?commitId ;
			 catalog:timeStamp ?timeStamp .

	FILTER (LCASE(?id) = @id)

	BIND (IRI(CONCAT(STR(?resource), "#", ?version)) AS ?resource_package)
    BIND (IRI(CONCAT(STR(?resource_package), "/", STRAFTER(STR(?dependencies), "#"))) AS ?resource_dependencies)
	BIND (IRI(CONCAT(STR(?resource_package), "/", STRAFTER(STR(?dependency_group), "#"))) AS ?resource_dependency_group)
	BIND (IRI(CONCAT(STR(?resource_package), "/", STRAFTER(STR(?dependency_group_property), "#"))) AS ?resource_dependency_group_property)
	BIND (IRI(CONCAT(STR(?resource_package), "/", STRAFTER(STR(?dependency), "#"))) AS ?resource_dependency)
	BIND (IRI(CONCAT(STR(?resource_package), "/", STRAFTER(STR(?dependency_property), "#"))) AS ?resource_dependency_property)

	OPTIONAL
	{
        ?package nuget:dependencies ?dependencies .
		?dependencies nuget:group ?dependency_group .
		?dependency_group nuget:dependency ?dependency .

		OPTIONAL { ?dependency_group nuget:targetFramework ?dependency_group_targetFramework . }
		OPTIONAL { ?dependency_group nuget:name ?dependency_group_name . }
		OPTIONAL { ?dependency_group nuget:property ?dependency_group_property .
			       ?dependency_group_property nuget:name ?dependency_group_property_name ;
								              nuget:value ?dependency_group_property_value . }
		
		?dependency nuget:id ?dependency_id .
		OPTIONAL { ?dependency nuget:range ?dependency_version . }

		OPTIONAL { ?dependency nuget:property ?dependency_property .
			       ?dependency_property nuget:name ?dependency_property_name ;
								        nuget:value ?dependency_property_value . }
	}
}
